apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        maven { 
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" 
		}
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7' 
	}
}

repositories {
    maven { 
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" 
	}
    mavenLocal()
}

group 'org.google'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.16.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.5.1'
def protocVersion = '3.5.1-1'

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
    compile "io.grpc:grpc-alts:${grpcVersion}"
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

// Inform to IDEA where srcs are locate
idea {
    module {
        sourceDirs += file('src/main/java')
        sourceDirs += file('build/generated/source/proto/main/grpc')
        sourceDirs += file('build/generated/source/proto/main/java')
        testSourceDirs += file('src/test/java')

        jdkName = '1.8'
    }
}
